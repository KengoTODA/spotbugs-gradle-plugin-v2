plugins {
    id 'java-gradle-plugin'
    id 'jacoco'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'com.diffplug.gradle.spotless' version '3.27.0'
    id 'net.ltgt.errorprone' version '1.1.1'
    id 'se.patrikerdes.use-latest-versions' version '0.2.13'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'org.sonarqube' version '2.8'
    id 'com.gtramontina.ghooks.gradle' version '1.1.1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'com.github.spotbugs.snom'

repositories {
    // To download the Android Gradle Plugin
    google()
    // To download trove4j required by the Android Gradle Plugin
    jcenter()
}

ext {
    junitVersion = '5.5.2'
    errorproneVersion = '2.3.4'
    spotBugsVersion = '3.1.11'
    slf4jVersion = '2.0.0-alpha1'
    immutablesVersion = '2.8.3'
    androidGradlePluginVersion = '3.5.3'
}

dependencies {
    errorprone "com.google.errorprone:error_prone_core:${errorproneVersion}"
    compileOnly "com.github.spotbugs:spotbugs:${spotBugsVersion}"
    compileOnly "org.immutables:value:${immutablesVersion}:annotations"
    compileOnly "com.android.tools.build:gradle:${androidGradlePluginVersion}"
    annotationProcessor "org.immutables:value:${immutablesVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testImplementation gradleTestKit()
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

test {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors()
}

javadoc {
    title = 'SpotBugs Gradle Plugin'
    options.with {
        addBooleanOption('html5', true)
        links 'https://docs.gradle.org/current/javadoc/'
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        links 'http://docs.groovy-lang.org/latest/html/api/'
    }
}

task processVersionFile(type: WriteProperties) {
    outputFile file('src/main/resources/spotbugs-gradle-plugin.properties')

    property 'slf4j-version', slf4jVersion
    property 'spotbugs-version', spotBugsVersion
}
tasks.processResources.dependsOn processVersionFile

apply from: "$rootDir/gradle/functional-test.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
apply from: "$rootDir/gradle/errorprone.gradle"
apply from: "$rootDir/gradle/publish.gradle"
apply from: "$rootDir/gradle/sonarcloud.gradle"

defaultTasks 'spotlessApply', 'build'
